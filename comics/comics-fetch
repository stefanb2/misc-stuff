#!/usr/bin/perl -w
use 5.030;
use strict;
use warnings;
use open qw(:encoding(UTF-8) :std);

#
# External programs called from this scriot
#
#    magick        (ImageMagick v7 with AVIF support)
#    ebook-convert (calibre)
#
# from perl-TimeDate
use Date::Format;
use File::Basename qw(basename);
use File::Spec;
use File::Temp qw(tempdir);
use Getopt::Long;
# from perl-HTML-Tree
use HTML::TreeBuilder;
use JSON qw(decode_json);
# requires also LWP::Protocol::https
use LWP::UserAgent;
use MIME::Base64;
use MIME::Lite;
# requires also Authen::SASL
use Net::SMTP;
# don't override localtime()
use Time::Piece qw();
use URI::URL;

##############################################################################
#
# Misc. stuff
#
##############################################################################
sub fail($) {
    my($msg) = @_;
    die "$0: ABORT: ${msg}\n";
    # does not return
}

# working directory
my $tempdir;

use constant DEFAULT_FILENAME => "comics.mobi";

##############################################################################
#
# Configuration file
#
##############################################################################
my %config;
sub load_configuration() {
    open(my $fh, '<', "$ENV{HOME}/.comics-fetch.config")
        or die("can't open configuration file: $!");

    while (<$fh>) {
        # remove comments and skip empty lines
        chomp;
        s/\s*#.*$//;
        next if /^\s*$/;

        my($section, $key, $value) = /^\s*(\w+)\.(\w+)\s*=\s*(.+)$/
            or fail("broken config line $.: $_");
        $value =~ s/\s+$//;

        $config{$section}->{$key} = $value;
    }

    close($fh);

    #foreach my $section (sort keys %config) {
    #    foreach my $key (sort keys %{ $config{$section} }) {
    #        my $value = $config{$section}->{$key};
    #        say "${section} ${key} '${value}'";
    #    }
    #}
}

##############################################################################
#
# Time base
#
##############################################################################
{
    # by default use script start time
    my $time = time();

    sub get_time() {
        return $time;
    }

    sub set_time($) {
        my($date) = @_;
        return unless $date;

        # interpret ISO-8601 YYYY-MM-DD as local time
        my $t = Time::Piece::localtime->strptime($date, "%Y-%m-%d");

        $time = $t->epoch();
    }
}

##############################################################################
#
# Comics configuration
#
##############################################################################

{
    my $code;

    sub load_comics() {
        use FindBin;
        my $dir = File::Spec->catfile($FindBin::Bin, "${FindBin::Script}.d");
        #print "$dir\n";
        push(@INC, $dir);

        my $matcher = File::Spec->catfile($dir, '??_*.pm');
        #print "$matcher\n";

        my @lines;
        foreach my $match (sort glob($matcher)) {
            my(undef, undef, $module) = File::Spec->splitpath($match);
            my($package) = $module =~ /^\d{2}_(\w+)\.pm$/
                or fail("comics module with invalid name '${module}'");
            #print "$module\n";

            require $module;
            push(@lines, "ComicsFetch::${package}::get();\n");
        }
        fail("couldn't find any comics modules")
            unless @lines;
        #print @lines;

        $code = eval "sub { @lines }"
            or fail("can't compile fetch code: $@");
    }

    sub fetch_comics() {
        $code->();
    }
}

##############################################################################
#
# HTTP handling
#
##############################################################################
{
    my $ua;

    sub http_init() {
        $ua = LWP::UserAgent->new();
        $ua->env_proxy();
        # some pages want us to fake it, so there you go...
        $ua->agent($config{http}->{useragent});
        $ua->default_header('Accept-Encoding' => scalar(HTTP::Message::decodable()));
        #$ua->show_progress(0);
    }

    sub get($;$) {
        my($url, $timeout) = @_;
        my $retries = $config{http}->{retries};
        my $r;

        $ua->timeout($timeout // $config{http}->{timeout});
        while ($retries--) {
            $r = $ua->get($url);
            if ($r->is_success) {
                #print STDERR "HTTP REQUEST: \n", $r->request->as_string, "\n";
                #print STDERR "HTTP RESPONSE: \n", $r->headers_as_string, "\n";
                #print STDERR "LENGTH: ", length($r->content), "\n";
                #print STDERR "HTML: \n", $r->content, "\n";
                my $content = $r->decoded_content;
                #print STDERR "HTML: \n", $content, "\n";
                return(defined($content) ? $content : $r->content);
            }
        }

        print STDERR "HTTP fetch error: ", $r->status_line, "\n";
        return;
    }
}

##############################################################################
#
# Image conversion
#
##############################################################################
sub convert_image_file($$) {
    my($base, $tempfile) = @_;

    # extract image information
    if (open(my $fh, '-|', 'magick', $tempfile, qw{-identify /dev/null})) {
        my $line = <$fh>;
        close($fh);

        if (my($x, $y) = ($line =~ /\s(\d+)x(\d+)\s/)) {
            my $basename = "${base}." . $config{image}->{type};
            my @options = $config{image}->{options} ? split(' ', $config{image}->{options}) : ();
            # auto-rotation detect
            push(@options, -rotate => $config{image}->{rotate})
                if ($x > $y);
            push(@options, -scale => $config{image}->{scale});
            #print STDERR "magick: @{options} ${basename}\n";

            if (system(
                    'magick',
                    $tempfile,
                    @options,
                    File::Spec->catfile($tempdir, $basename)
                ) == 0) {
                return $basename;
            } else {
                print STDERR "Picture conversion failed: $!\n";
            }
        } else {
            print STDERR "Couldn't extract image dimensions: ${line}";
        }
    } else {
        print STDERR "Couldn't start image identifier: $!\n";
    }

    return;
}

sub convert_image($$) {
    my($base, $content) = @_;
    my $tempfile = "${tempdir}/temp.img";

    # dump URL content as binary data to temporary file for conversion
    if (open(my $fh, '>:raw', $tempfile)) {
        print $fh $content;

        if (close($fh)) {
            return convert_image_file($base, $tempfile);
        } else {
            print STDERR "Couldn't save image to file: $!\n";
        }
    } else {
        print STDERR "Couldn't open image file: $!\n";
    }

    return;
}

##############################################################################
#
# HTML generation
#
##############################################################################
{
    my @nodes;

    sub get_nodes() {
        return \@nodes;
    }

    sub create_node() {
        return HTML::Element->new('div');
    }

    sub add_node($) {
        my($node) = @_;
        my $html = $node->as_HTML();
        #print STDERR "HTML: ${html}\n";
        # add empty H1 as page break
        push(@nodes, "${html}\n<h1/>\n");
    }

    sub add_image_node($$) {
        my($filename, $extra) = @_;
        my $node = create_node();
        $node->push_content(
            HTML::Element->new('img',
                src => $filename,
                %{$extra}
            )
        );
        add_node($node);
    }

    sub add_error_node($$) {
        my($base, $msg) = @_;
        my $node = create_node();
        $node->push_content(
            HTML::Element->new('p')
                ->push_content("${base}: ${msg}")
        );
        add_node($node);
    }
}

##############################################################################
#
# Low-level comics content handling
#
##############################################################################
# get a single image for URL from image URL
sub get_image($$$;$) {
    my($url, $base, $imgurl, $extra) = @_;

    #print STDERR "IMAGE URL: ${imgurl}\n";
    if (my $content = get($imgurl)) {
        if (my $basename = convert_image($base, $content)) {
            add_image_node($basename, $extra);
            print "comic fetched from '$url'\n";
            return;
        }
    } else {
        print STDERR "Couldn't get page '${imgurl}': $!\n";
    }

    add_error_node($base, "couldn't fetch image");
}

# get a single comic for URL
sub get_comic($$$$) {
    my($url, $base, $timeout, $code) = @_;

    #print STDERR "COMIC URL: ${url}\n";
    my $content = get($url, $timeout);
    unless ($content) {
        print STDERR "Couldn't get page '${url}': $!\n";
        add_error_node($base, "couldn't get page");
        return;
    }

    my $tree = HTML::TreeBuilder->new();
    # include HTML5 tags like <picture>
    $tree->ignore_unknown(0);
    $tree->parse($content);
    $tree->eof();
    unless (defined($tree)) {
        print STDERR "Couldn't parse HTML from '${url}': $!\n";
        add_error_node($base, "couldn't get HTML");
        return;
    }
    # print STDERR "DUMP: "; $tree->dump(*STDERR);

    # pass DOM tree to callback to extract image URL
    if (my($imgurl, $extra) = $code->($tree)) {
        get_image($url, $base, $imgurl, $extra);
    } else {
        print STDERR "Couldn't find image in '${url}'\n";
        add_error_node($base, "couldn't find image");
    }

    # always release DOM tree after we're done!
    $tree->delete();
}

##############################################################################
#
# Support for some comics pages
#
##############################################################################
# simple case:
# - look for a DOM node
# - locate the IMG tag inside it
# - generate URL from the image SRC attribute
sub get_comic_simple($$$@) {
    my($url, $base, $timeout, @lookdown) = @_;

    get_comic(
        $url,
        $base,
        $timeout,
        sub {
            my($tree) = @_;
            my $url;

            my $node = $tree->look_down(@lookdown);
            if ($node) {
                my $img = $node->find('img');
                ($url = $img->attr('src'))
                    =~ s,^//,https://, if $img;
            }

            return($url ? $url : ());
        }
    );
}

# www.gocomics.com
sub get_comic_gocomics($) {
    my($base)     = @_;
    my @yesterday = localtime(get_time() - 86400);
    my $url       = strftime("https://www.gocomics.com/${base}/%Y/%m/%d",
                             @yesterday);

    get_comic(
        $url,
        $base,
        undef,
        sub {
            my($tree) = @_;
            my $url;

            # page now loads the main image dynamically, but
            # the URL can be found in the Open Graph metadata.
            my $meta = $tree->look_down(
                _tag     => 'meta',
                property => "og:image",
            );
            if ($meta) {
                $url = $meta->attr("content");
            }

            return($url ? $url : ());
        }
    );
}

# Helsingin Sanomat
sub get_comic_hs($$) {
    my($api_url, $base) = @_;
    my $imgurl;

    eval {
        # fetch API response
        my $json = get($api_url);
        die "no response from API\n" unless ($json);

        # decode JSON response & extract data
        my $array                 = decode_json($json);
        my $picture               = $array->[0]->{picture};
        my($url_template, $width) = @{$picture}{qw(url width)};

        # generate image URL from response - requires AVIF support
        #
        # $ magick -list format | grep -i -e avif -e heic
        # ...
        # AVIF  HEIC      rw+   AV1 Image File Format (1.17.6)
        #
        ($imgurl = $url_template) =~ s,/WIDTH\.EXT$,/${width}.avif,;
    };
    if ($@) {
        print STDERR "Couldn't get response from '${api_url}': $@";
        add_error_node($base, "couldn't get page");
        return;
    }

    get_image($api_url, $base, $imgurl);
}

# King Features
{
    sub _kingfeatures_parse($) {
        my($tree) = @_;

        # find first comic reader on page
        my($reader) = $tree->look_down(
            _tag  => 'div',
            class => qr/comic-reader-item/,
        );
        unless (defined($reader)) {
            print STDERR "couldn't find reader\n";
            return;
        }

        # find panels in reader
        my @panels = $reader->look_down(
            _tag  => 'div',
            class => qr/ck-panel/,
        );
        unless (@panels) {
            print STDERR "couldn't find panels\n";
            return;
        }

        # extract images from panels
        my @images;
        my $max_height = 0;
        foreach my $panel (@panels) {
            my $style = $panel->attr('style');
            my $img   = $panel->look_down(_tag => 'img');
            unless (defined($style) && defined($img)) {
                print STDERR "couldn't find image in panel\n";
                return;
            }

            # image size and base URL
            my($width, $height) = $style =~ m,aspect-ratio:\s*(\d+)\s*/\s*(\d+),;
            my $src             = $img->attr('src');
            unless (defined($width) && defined($height) && defined($src)) {
                print STDERR "invalid image in panel\n";
                return;
            }

            my $url = URI::URL->new($src);
            unless (defined($url)) {
                print STDERR "invalid base URL in panel\n";
                return;
            }

            my %query = $url->query_form();
            unless (%query && defined($query{url})) {
                print STDERR "unexpected base URL in panel\n";
                return;
            }

            # extract image URL from query
            $url = URI::URL->new($query{url});
            unless (defined($url)) {
                print STDERR "invalid image URL in panel\n";
                return;
            }

            # valid image found -> append information to list
            #print "aspect: ${width} x ${height} -> ${url}\n";
            $width  = int($width);
            $height = int($height);
            push(@images, {
                height => $height,
                url    => $url,
                width  => $width,
            });

            # heighest image determines height of merged image
            if ($height > $max_height) {
                $max_height = $height;
            }
        }

        return \@images, $max_height;
    }

    sub _kingfeatures_fetch_and_merge($$$) {
        my($base, $images, $max_height) = @_;
        my @cmd                         = qw(magick);

        while (my($index, $image) = each @{$images}) {
            my($height, $url, $width) = @{$image}{qw(height url width)};
            my $content = get($url);
            unless (defined($content)) {
                print STDERR "couldn't fetch image from ${url}\n";
                return;
            }

            # dump URL content as binary data to temporary file for merging
            my $tempfile = "${tempdir}/${base}_${index}.jpg";
            if (open(my $fh, '>:raw', $tempfile)) {
                print $fh $content;

                if (close($fh)) {
                    # add image to sequence
                    push(@cmd, $tempfile);

                    # resize image if necessary
                    if ($height ne $max_height) {
                        my $factor = $max_height / $height;
                        $width  = int($width  * $factor);
                        $height = int($height * $factor);
                        push(@cmd, '-resize', "${width}x${height}");
                    }

                } else {
                    print STDERR "can't write data to temporary file for merging\n";
                    return;
                }
            } else {
                print STDERR "can't open temporary file for merging\n";
                return;
            }

            print "${url}\n";
        }

        my $merged = "${tempdir}/${base}_merged.jpg";
        push(@cmd, '+append', $merged);
        if (system(@cmd) != 0) {
            print STDERR "can't merge images\n";
            return;
        }

        return $merged;
    }

    sub get_comic_kingfeatures($) {
        my($base) = @_;
        my @yesterday = localtime(get_time() - 86400);

        # fetch & parse page contents
        my $url = strftime("https://comicskingdom.com/${base}/%Y-%m-%d", @yesterday);
        my $html = get($url);
        my $tree = HTML::TreeBuilder->new_from_content($html);
        unless (defined($tree)) {
            print STDERR "Couldn't parse HTML from '${url}': $!\n";
            add_error_node($base, "couldn't get HTML");
            return;
        }

        my($images, $max_height) = _kingfeatures_parse($tree);

        # always release DOM tree after we're done!
        $tree->delete();

        unless (defined($images) && defined($max_height) && ($max_height > 0)) {
            add_error_node($base, "couldn't parse page");
            return;
        }

        if (@{$images} == 1) {
            # single panel -> no need to merge
            get_image($url, $base, $images->[0]->{url});

        } else {
            # multiple panels -> merge horizontally to a single image
            my $merged = _kingfeatures_fetch_and_merge($base, $images, $max_height);
            unless (defined($merged)) {
                add_error_node($base, "couldn't create merged image");
                return;
            }

            my $basename = convert_image_file($base, $merged);
            unless (defined($basename)) {
                add_error_node($base, "couldn't convert image");
                return;
            }

            add_image_node($basename, undef);
            print "comic fetched from '${url}'\n";
        }
    }
}

##############################################################################
#
# E-Book generation
#
##############################################################################
sub generate_ebook() {
    my $index = File::Spec->catfile($tempdir, 'index.html');

    if (open(my $fh, '>', $index)) {
        my @local   = localtime(get_time());
        my $date    = strftime("%e-%b-%Y", @local);
        my @nodes   = @{ get_nodes() };
        my $content = <<"END_OF_HTML";
<!DOCTYPE html>
<head>
  <title>Comics ${date}</title>
  <meta http-equiv="Content-Language" content="en" />
</head>
<body>
 @nodes
 End of document
</body>
END_OF_HTML

        #print $content;
        print $fh $content;
        if (close($fh)) {
            print "converting...\n";
            if (system(
                'ebook-convert', $index, $config{ebook}->{filename},
                '--authors=' . $config{ebook}->{author},
                '--output-profile=' . $config{ebook}->{profile}
            ) == 0) {
                return 1;
            } else {
                print STDERR "conversion failed: $!\n";
            }
        } else {
            print STDERR "couldn't write index page: $!\n";
        }
    } else {
        print STDERR "couldn't open index page: $!\n";
    }

    return;
}

##############################################################################
#
# Mail handling
#
##############################################################################
sub send_mail() {
    my $file = $config{ebook}->{filename};
    my $from = $config{mail}->{from};
    my $to   = $config{mail}->{to};

    # generate MIME message
    my $msg = MIME::Lite->new(
        From    => $from,
        To      => $to,
        Subject => $config{mail}->{subject},
        Type    => 'multipart/mixed',
    );
    $msg->attach(
        Type    => 'TEXT',
        Data    => $config{mail}->{greeting},
    );
    $msg->attach(
        Type        => $config{mail}->{type},,
        Path        => $file,
        Filename    => basename($file),
        Disposition => 'attachment',
    );

    # SMTP client
    my $smtp = Net::SMTP->new($config{mail}->{host},
        Port     => $config{mail}->{port},
        #Debug    => 1,
        Timeout  => $config{mail}->{timeout},
    );
    if ($smtp) {
        if ($smtp->starttls()  &&
            # Requires Authen::SASL
            $smtp->auth($from, $config{mail}->{password}) &&
            $smtp->mail($from) &&
            $smtp->to($to)     &&
            $smtp->data()      &&
            $smtp->datasend($msg->as_string()) &&
            $smtp->dataend()   &&
            $smtp->quit()
        ) {
            print "mail sent\n";
        } else {
            print STDERR "can't send mail: " . $smtp->message() . "\n";
        }
    } else {
        print STDERR "can't connect to SMTP server: $@\n";
    }
}

##############################################################################
#
# Main program
#
##############################################################################

my %Options = (
    date => undef,
    mail => 1,
);

GetOptions(\%Options,
    "date:s",
    "mail!",
) or fail("can't parse command line options");
set_time($Options{date});
load_configuration();
load_comics();
http_init();
$tempdir = tempdir(CLEANUP => 1)
    or fail("can't create temorary directory: $!");
print "Temporary directory '${tempdir}'\n";

fetch_comics();
#print @{ get_nodes() };

if (generate_ebook()) {
    send_mail()
        if $Options{mail};
}

# That's all folks
exit 0;
